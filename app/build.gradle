apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 34

    def buildNum = 0
    defaultConfig {
        applicationId "com.grzwolf.grzlog"
        minSdkVersion 29
        targetSdkVersion 34

        // the one & only places to change the app version name and code version
        versionCode 1       // playstore relevant - increase it to prevent a downgrade
        versionName "1.1.6" // user relevant

        //
        def buildTime = new Date()
        versionNameSuffix = "${buildTime.format(' yyyy/MM/dd__HH:mm:ss')}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "$applicationId v$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "$defaultConfig.applicationId v${defaultConfig.versionName} ${defaultConfig.versionNameSuffix}"
            resValue "string", "tag_version", "v${defaultConfig.versionName}"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            // memorize build number
            buildConfigField("int", "BUILD_NUM", "${buildNum}")
            def nameVer = ''
            def codeVer = 1
            def Properties versionProps = new Properties()
            def versionPropsFile = file('version.properties')
            if(versionPropsFile.exists()) {
                versionProps.load(new FileInputStream(versionPropsFile))
                codeVer = (versionProps['VERSION_CODE'] ?: "0").toInteger()
                nameVer = (versionProps['VERSION_NAME'] ?: "")
                // if name or code change, start build number from 0
                if (!nameVer.equals(defaultConfig.versionName) || codeVer != defaultConfig.versionCode) {
                    buildNum = 0
                } else {
                    buildNum = (versionProps['BUILD_NUM'] ?: "0").toInteger() + 1
                }
                // max allowed value, otherwise exception
                if(buildNum >= 2000000000) {
                    buildNum = 0
                }
            }
            versionProps['BUILD_NUM'] = buildNum.toString()
            versionProps['VERSION_NAME'] = defaultConfig.versionName
            versionProps['VERSION_CODE'] = defaultConfig.versionCode.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
            resValue "string", "app_version", "$defaultConfig.applicationId v${defaultConfig.versionName}-vc${defaultConfig.versionCode}-#${buildNum} debug ${defaultConfig.versionNameSuffix}"
            resValue "string", "tag_version", "v${defaultConfig.versionName}"
        }
    }
    namespace 'com.grzwolf.grzlog'
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // w/o this section: duplicate class error --> https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android?page=1&tab=scoredesc#tab-top
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
    implementation "androidx.browser:browser:1.7.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'androidx.databinding:databinding-runtime:8.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // next line needs the above constraints {..} section
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // next line needs the above constraints {..} section
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.13.2'
    // gps
    implementation 'com.google.android.gms:play-services:12.0.1'
    // in app update from GitHub
    implementation 'com.android.volley:volley:1.2.1'
}
repositories {
    mavenCentral()
}
