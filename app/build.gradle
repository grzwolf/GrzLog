apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 36

    // Execution failed for task ':app:compileDebugKotlin'. > Inconsistent JVM Target Compatibility Between Java and Kotlin Tasks
    // kudos: https://stackoverflow.com/questions/77520506/inconsistent-jvm-target-compatibility-detected-for-tasks-despite-already-definin
    compileOptions {
        // not needed anymore, remove it
        //sourceCompatibility = JavaVersion.VERSION_22
        //targetCompatibility = JavaVersion.VERSION_22
    }
    kotlin {
        // adding this to solve it
        jvmToolchain(22)
    }

    def buildNum = 0
    defaultConfig {
        applicationId "com.grzwolf.grzlog"
        minSdkVersion 29
        targetSdkVersion 36

        // the one & only places to change the app version name and code version
        versionCode 1        // playstore relevant - increase it to prevent a downgrade
        versionName "1.1.54" // user relevant

        //
        def buildTime = new Date()
        versionNameSuffix = "${buildTime.format(' yyyy/MM/dd__HH:mm:ss')}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.base.archivesName = "$applicationId v$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_version", "$defaultConfig.applicationId v${defaultConfig.versionName} ${defaultConfig.versionNameSuffix}"
            resValue "string", "tag_version", "v${defaultConfig.versionName}"
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            // memorize build number
            buildConfigField("int", "BUILD_NUM", "${buildNum}")
            def nameVer = ''
            def codeVer = 1
            def Properties versionProps = new Properties()
            def versionPropsFile = file('version.properties')
            if(versionPropsFile.exists()) {
                versionProps.load(new FileInputStream(versionPropsFile))
                codeVer = (versionProps['VERSION_CODE'] ?: "0").toInteger()
                nameVer = (versionProps['VERSION_NAME'] ?: "")
                // if name or code change, start build number from 0
                if (!nameVer.equals(defaultConfig.versionName) || codeVer != defaultConfig.versionCode) {
                    buildNum = 0
                } else {
                    buildNum = (versionProps['BUILD_NUM'] ?: "0").toInteger() + 1
                }
                // max allowed value, otherwise exception
                if(buildNum >= 2000000000) {
                    buildNum = 0
                }
            }
            versionProps['BUILD_NUM'] = buildNum.toString()
            versionProps['VERSION_NAME'] = defaultConfig.versionName
            versionProps['VERSION_CODE'] = defaultConfig.versionCode.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
            resValue "string", "app_version", "$defaultConfig.applicationId v${defaultConfig.versionName}-vc${defaultConfig.versionCode}-#${buildNum} debug ${defaultConfig.versionNameSuffix}"
            resValue "string", "tag_version", "v${defaultConfig.versionName}"
        }
    }
    namespace 'com.grzwolf.grzlog'
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'androidx.biometric:biometric:1.2.0-alpha05'
    implementation "androidx.browser:browser:1.9.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'androidx.databinding:databinding-runtime:9.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    implementation "androidx.core:core-ktx:1.17.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:5.0.5'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.android.volley:volley:1.2.1' // in app update from GitHub
    implementation("androidx.lifecycle:lifecycle-process:2.10.0")
    // defer photo picker implementation until min SDK >=34
    implementation("androidx.photopicker:photopicker:1.0.0-alpha01")
}
repositories {
    mavenCentral()
}
